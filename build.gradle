plugins {
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.google.osdetector' version '1.7.3'
    id 'jvm-test-suite'
    id 'com.vanniktech.maven.publish' version '0.35.0'
}

allprojects {
    repositories {
        mavenCentral()
    }
    apply plugin: 'jacoco'
}

subprojects {
    group = 'io.spiffe'
    version = project.version

    ext {
        grpcVersion = '1.77.0'
        jupiterVersion = '5.13.4'
        mockitoVersion = '4.11.0'
        lombokVersion = '1.18.42'
        nimbusVersion = '10.6'
        shadowVersion = '9.3.0'

        //IMPORTANT: This must be in sync with the shaded netty version in gRPC
        nettyVersion = '4.2.8.Final'
    }

    apply plugin: 'java-library'

    // vanniktech plugin (maven-publish + signing)
    apply plugin: 'com.vanniktech.maven.publish'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        withSourcesJar()
    }

    javadoc {
        exclude "**/grpc/**"
        exclude "**/internal/**"
    }

    // Central Portal + signing configuration
    mavenPublishing {
        // Credentials come from:
        //   - ORG_GRADLE_PROJECT_mavenCentralUsername
        //   - ORG_GRADLE_PROJECT_mavenCentralPassword
        publishToMavenCentral()
        signAllPublications()

        pom {
            name.set(project.name)
            url.set('https://github.com/spiffe/java-spiffe')

            // description is only available after evaluation
            afterEvaluate {
                if (project.description != null) {
                    description.set(project.description)
                }
            }

            scm {
                connection = 'scm:git:https://github.com/spiffe/java-spiffe.git'
                developerConnection = 'scm:git:git@github.com:spiffe/java-spiffe.git'
                url = 'https://github.com/spiffe/java-spiffe'
            }

            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                ['maxlambrecht:Max Lambrecht', 'rturner3:Ryan Turner'].each { devData ->
                    developer {
                        def devInfo = devData.split(':')
                        id = devInfo[0]
                        name = devInfo[1]
                        url = 'https://github.com/' + devInfo[0]
                        roles = ["Maintainer"]
                    }
                }
            }
        }
    }

    dependencies {
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
        implementation group: 'commons-validator', name: 'commons-validator', version: "1.10.1"

        testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${jupiterVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${jupiterVersion}"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${jupiterVersion}"

        testCompileOnly group: 'org.mockito', name: 'mockito-core', version: "${mockitoVersion}"
        testRuntimeOnly group: 'org.mockito', name: 'mockito-junit-jupiter', version: "${mockitoVersion}"

        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            testImplementation group: 'uk.org.webcompere', name: 'system-stubs-core', version: '2.0.3' // Last version supporting Java 8
        } else {
            testImplementation group: 'uk.org.webcompere', name: 'system-stubs-core', version: '2.1.8'
        }

        // Project Lombok dependency
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombokVersion}"
    }

    testing {
        suites {
            test {
                useJUnitJupiter()
            }
        }
    }

    afterEvaluate {
        // Work around Gradle 8.14.x implicit-dependency validation:
        // generateMetadataFileForMavenPublication must run after plainJavadocJar.
        tasks.matching { it.name == "generateMetadataFileForMavenPublication" }.configureEach {
            dependsOn(tasks.named("plainJavadocJar"))
        }
    }
}

task jacocoTestReport(type: JacocoReport) {
    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    // Filter out autogenerated or internal code
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/grpc/**', '**/exception/**', '**/internal/**'])
        }))
    }

    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestReport.dependsOn {
    subprojects.collectMany { project ->
        project.tasks.matching { it.name in ['test'] }
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    sourceDirs = ['java-spiffe-core/src/main/java',
                  'java-spiffe-helper/src/main/java',
                  'java-spiffe-provider/src/main/java']
}

// copy submodules jars to a common folder for deploy
task copyJars(type: Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from subprojects.collect { it.tasks.withType(Jar) }
    into "$buildDir/libs"
}

assemble.finalizedBy copyJars
