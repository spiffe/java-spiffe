plugins {
    id 'com.google.protobuf' version '0.9.5'
    id 'java-library'
    id 'java-test-fixtures'
}

description = "Core functionality to fetch, process and validate X.509 and JWT SVIDs and Bundles from the Workload API."

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }

    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')

        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

tasks.named('sourcesJar') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    integrationTestCompileOnly.extendsFrom(testCompileOnly)

    protocTool
    grpcTool
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    shouldRunAfter(tasks.named('test'))
}

def protocExe = "com.google.protobuf:protoc:3.25.5:${osdetector.classifier}@exe"
def grpcExe   = "io.grpc:protoc-gen-grpc-java:${grpcVersion}:${osdetector.classifier}@exe"

dependencies {
    protocTool protocExe
    grpcTool   grpcExe

    def osArch = System.getProperty("os.arch")

    if (osdetector.os.is('osx')) {
        if (osArch == "x86_64") {
            compileOnly(project('grpc-netty-macos'))
            testImplementation(project('grpc-netty-macos'))
        } else if (osArch == "aarch64") {
            compileOnly(project('grpc-netty-macos-aarch64'))
            testImplementation(project('grpc-netty-macos-aarch64'))
        } else {
            throw new GradleException("Architecture not supported: ${osArch}")
        }
    } else {
        compileOnly(project('grpc-netty-linux'))
        testImplementation(project('grpc-netty-linux'))
    }

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    testImplementation "io.grpc:grpc-inprocess:${grpcVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    implementation "com.nimbusds:nimbus-jose-jwt:${nimbusVersion}"
    testFixturesImplementation "com.nimbusds:nimbus-jose-jwt:${nimbusVersion}"

    testFixturesImplementation "org.bouncycastle:bcpkix-jdk15on:1.70"
    testFixturesImplementation "org.apache.commons:commons-lang3:3.20.0"
}

protobuf {
    protoc {
        path = configurations.protocTool.singleFile.absolutePath
    }
    plugins {
        grpc {
            path = configurations.grpcTool.singleFile.absolutePath
        }
    }
    generateProtoTasks {
        all().configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

import com.google.protobuf.gradle.GenerateProtoTask

def protocBin = configurations.protocTool.singleFile
def grpcBin   = configurations.grpcTool.singleFile

tasks.register("makeProtoToolsExecutable") {
    inputs.files(protocBin, grpcBin)
    doLast {
        protocBin.setExecutable(true, false)
        grpcBin.setExecutable(true, false)
    }
}

tasks.withType(GenerateProtoTask).configureEach {
    dependsOn(tasks.named("makeProtoToolsExecutable"))
}
