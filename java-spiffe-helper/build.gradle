plugins {
    id "com.github.johnrengelman.shadow" version "${shadowVersion}"
}

description = "Java SPIFFE Library Helper module to store X.509 SVIDs and Bundles in a Java KeyStore in disk"

apply plugin: 'com.github.johnrengelman.shadow'

assemble.dependsOn shadowJar

shadowJar {
    mergeServiceFiles()
    archiveClassifier = project.hasProperty('archiveClassifier') && project.archiveClassifier != "" ? project.archiveClassifier : osdetector.classifier
    manifest {
        attributes 'Main-Class': 'io.spiffe.helper.cli.Runner'
    }
}

dependencies {
    api(project(':java-spiffe-core'))

    // runtimeOnly grpc-netty dependency module will be included in the shadowJar
    if (osdetector.os.is('osx') ) {
        project.ext.osArch = System.getProperty("os.arch")
        if ("x86_64" == project.ext.osArch) {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos'))
        } else if ("aarch64" == project.ext.osArch) {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos-aarch64'))
        } else {
            throw new GradleException("Architecture not supported: " + project.ext.osArch)
        }
    } else {
        runtimeOnly(project(':java-spiffe-core:grpc-netty-linux'))
    }

    implementation group: 'commons-cli', name: 'commons-cli', version: '1.11.0'

    testImplementation(testFixtures(project(":java-spiffe-core")))
}
