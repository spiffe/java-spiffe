plugins {
    id "com.gradleup.shadow" version "${shadowVersion}"
}

description = "Java SPIFFE Library Helper module to store X.509 SVIDs and Bundles in a Java KeyStore in disk"

tasks.named('assemble') {
    dependsOn(tasks.named('shadowJar'))
}

shadowJar {
    mergeServiceFiles()

    archiveClassifier = (project.findProperty('archiveClassifier')?.toString()?.trim())
            ? project.property('archiveClassifier').toString()
            : osdetector.classifier

    manifest {
        attributes 'Main-Class': 'io.spiffe.helper.cli.Runner'
    }
}

dependencies {
    api(project(':java-spiffe-core'))

    def osArch = System.getProperty("os.arch")

    if (osdetector.os.is('osx')) {
        if (osArch == 'x86_64') {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos'))
        } else if (osArch == 'aarch64') {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos-aarch64'))
        } else {
            throw new GradleException("Architecture not supported: ${osArch}")
        }
    } else {
        runtimeOnly(project(':java-spiffe-core:grpc-netty-linux'))
    }

    implementation "commons-cli:commons-cli:1.11.0"

    testImplementation(testFixtures(project(":java-spiffe-core")))
}
