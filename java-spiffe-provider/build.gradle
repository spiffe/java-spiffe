plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

description = "Java Security Provider implementation supporting X.509-SVIDs and methods for " +
              "creating SSLContexts that are backed by the Workload API."

apply plugin: 'com.github.johnrengelman.shadow'

assemble.dependsOn shadowJar

shadowJar {
    archiveClassifier = "all-".concat(osdetector.classifier)
}

dependencies {
    api(project(":java-spiffe-core"))

    // runtimeOnly grpc-netty dependency module will be included in the shadowJar
    if (osdetector.os.is('osx') ) {
        project.ext.osArch = System.getProperty("os.arch")
        if ("x86_64" == project.ext.osArch) {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos'))
        } else if ("aarch64" == project.ext.osArch) {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos-aarch64'))
        } else {
            throw new GradleException("Architecture not supported: " + project.ext.osArch)
        }
    } else {
        runtimeOnly(project(':java-spiffe-core:grpc-netty-linux'))
    }

    testImplementation(testFixtures(project(":java-spiffe-core")))
}
