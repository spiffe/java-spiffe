plugins {
    id "com.gradleup.shadow" version "${shadowVersion}"
}

description = "Java Security Provider implementation supporting X.509-SVIDs and methods for " +
        "creating SSLContexts that are backed by the Workload API."

tasks.named('assemble') {
    dependsOn(tasks.named('shadowJar'))
}

shadowJar {
    mergeServiceFiles()
    archiveClassifier = "all-${osdetector.classifier}"
}

dependencies {
    api(project(":java-spiffe-core"))

    // runtimeOnly grpc-netty dependency module will be included in the shadowJar
    def osArch = System.getProperty("os.arch")

    if (osdetector.os.is('osx')) {
        if (osArch == 'x86_64') {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos'))
        } else if (osArch == 'aarch64') {
            runtimeOnly(project(':java-spiffe-core:grpc-netty-macos-aarch64'))
        } else {
            throw new GradleException("Architecture not supported: ${osArch}")
        }
    } else {
        runtimeOnly(project(':java-spiffe-core:grpc-netty-linux'))
    }

    testImplementation(testFixtures(project(":java-spiffe-core")))
}
